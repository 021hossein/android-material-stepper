apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion project.androidCompileSdkVersion
    buildToolsVersion ("$androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile ("com.android.support:appcompat-v7:$androidSupportLibraryVersion")
}

def repositoryUrl = hasProperty('repositoryUrl') ? getProperty('repositoryUrl') : '';
def repositoryUserName = hasProperty('repositoryUserName') ? getProperty('repositoryUserName') : '';
def repositoryPassword = hasProperty('repositoryPassword') ? getProperty('repositoryPassword') : '';

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
        return; // Skip debug builds.
    }
    task("javadoc${variant.name.capitalize()}", type: Javadoc) {
        title = "Material Stepper"
        description 'Generates Javadoc for $variant.name.'
        source = android.sourceSets.main.java.srcDirs
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }

}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: "javadocRelease") {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        repository (url: "$repositoryUrl") {
            authentication(userName: "$repositoryUserName", password: "$repositoryPassword")
        }

        pom {
            version = POM_VERSION
            artifactId = POM_ARTIFACT_ID
            groupId = POM_GROUP_ID
        }
    }
}
